plugins {
	id 'java'
	id 'application'

	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.7.RELEASE'

	id 'com.github.psxpaul.execfork' version '0.1.10'

	id 'maven-publish'
	id 'org.ajoberstar.grgit' version '3.1.1'
	id 'com.jfrog.artifactory' version '4.9.6'

	id 'idea'
	id 'eclipse'
	id 'com.github.ben-manes.versions' version '0.21.0'
}

description = 'census-fwmt-performance-suite'
group = 'uk.gov.ons.census.fwmt'
version = '1.0.1-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

mainClassName = 'uk.gov.ons.census.fwmt.performancesuite.Application'

repositories {
	mavenLocal()
	if (!project.hasProperty('omitSDCArtifactory')) {
		maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-release' }
		maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-snapshot' }
	}
	mavenCentral()
}

dependencies {
	compile 'uk.gov.ons.census.fwmt:census-fwmt-events:1.0.6-RELEASE'
	compile 'uk.gov.ons.census.fwmt:census-fwmt-common:1.0.12-RELEASE'

	compile 'org.springframework.boot:spring-boot-starter-actuator:2.1.4.RELEASE'

	compile 'com.opencsv:opencsv:4.6'

	compile 'io.springfox:springfox-swagger2:2.9.2'
	compile 'io.springfox:springfox-swagger-ui:2.9.2'

	compile 'org.projectlombok:lombok:1.18.6'
	annotationProcessor 'org.projectlombok:lombok:1.18.6'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.6'

	implementation 'org.springframework.boot:spring-boot-starter-web:2.1.4.RELEASE'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.4.RELEASE'
}

springBoot {
	buildInfo {
		properties {
			additional = [
					'Git-Commit': grgit.head().abbreviatedId
			]
		}
	}
}

artifactory {
	contextUrl = System.getenv('ARTIFACTORY_URL')
	publish {
		repository {
			repoKey = 'libs-snapshot-local'
			username = System.getenv('ARTIFACTORY_USERNAME')
			password = System.getenv('ARTIFACTORY_PASSWORD')
		}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = group
			artifactId = 'census-fwmt-performancesuite'
			version = version

			from components.java
		}
	}
}

artifactoryPublish {
	publications 'maven'
}

